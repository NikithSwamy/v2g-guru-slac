/*
MIT License

Copyright (c) 2021 Tomas Prerovsky

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.
*/


kind Event;
kind Guard;
kind Systemstate;
Guard MMEPluginDebug;
Event evMMEPluginInitialized;

MMEPluginDebug = 1;


val TP_EV_SLAC_init = 1.0*s;


macro include_homeplug_mme_handler{

sm{
    map_mme_to_events;
    states{Initial;};
    on_enter{
        homeplug_mme_handler( 
            setup {
                communication{
                    port{"56800";};
                    host{"localhost";};
                    as_server{0;};
                };
                on_initplugin{
                    evMMEPluginInitialized;
                };
                on_start_tests{
                    evMMEPluginTestStarted;
                };
                on_end_tests{
                    evMMEPluginTestFinished;
                };
                on_error{
                    evMMEPluginError;
                };
                run_tests{
                    //all;
                };
                on_message{
                    if(CM_SLAC_PARM_REQ == mme_type){
                        on_mme_cm_slac_parm_eq{};
                    } 
                    else if(CM_SLAC_PARM_CNF == mme_type){
                        on_mme_cm_slac_parm_cnf{};
                    }
                    else if(CM_START_ATTEN_CHAR_IND == mme_type){
                        on_mme_cm_start_atten_char_ind{};
                    }
                    else if(CM_MNBC_SOUND_IND == mme_type){
                        on_mme_cm_mnbc_sound_ind{};
                    }else if(CM_ATTEN_PROFILE_IND == mme_type)
                    {
                        on_mme_cm_atten_profile_ind{};
                    }else if(CM_ATTEN_CHAR_RSP == mme_type)
                    {
                        on_mme_cm_atten_char_rsp{};
                    }else if(CM_VALIDATE_REQ == mme_type)
                    {
                        on_mme_cm_validate_req{};
                    }else if(CM_VALIDATE_CNF == mme_type)
                    {
                        on_mme_cm_validate_cnf{};
                    }else if(CM_SLAC_MATCH_REQ == mme_type)
                    {
                        on_mme_cm_slac_match_req{};
                    }else if(CM_SLAC_MATCH_CNF == mme_type)
                    {
                        on_mme_cm_slac_match_cnf{};
                    }else if(CM_SET_KEY_REQ == mme_type)
                    {
                        on_mme_cm_set_key_req{};
                    }else if(CM_SET_KEY_CNF == mme_type)
                    {
                        on_mme_cm_set_key_cnf{};
                    }else if(CM_AMP_MAP_REQ == mme_type)
                    {
                        on_mme_cm_amp_map_req{};                    
                    }else if(CM_AMP_MAP_CNF == mme_type)
                    {
                        on_mme_cm_amp_map_cnf{};
                    } else if ( CM_ATTEN_CHAR_IND == mme_type) {
                        on_mme_cm_atten_char_ind{};
                    }       
                };            
            } 
        );
    };

    Actions{
        onMMEPluginInitialized{
            print("MME Plugin ready\n");
        };
    };

    t{Initial;Initial;evMMEPluginInitialized;MMEPluginDebug;onMMEPluginInitialized;};
};
};

macro run_include_homeplug_mme_handler{
    Start{map_mme_to_events;};    
};

include_homeplug_mme_handler{};

Systemstate curRunId0,curRunId1,curRunId2,curRunId3,curRunId4,curRunId5,curRunId6,curRunId7;
curRunId0 = 0;
curRunId1 = 0;
curRunId2 = 0;
curRunId3 = 0;
curRunId4 = 0;
curRunId5 = 0;
curRunId6 = 0;
curRunId7 = 0;

macro next_run_id{
    curRunId0 = curRunId0 + 1;
    if (curRunId0 > 255) {
        curRunId0 = 0;
        curRunId1 = curRunId1 + 1;
        if (curRunId1 > 255) {
            curRunId1 = 0;
            curRunId2 = curRunId2 + 1;
            if (curRunId2 > 255) {
                curRunId2 = 0;
                curRunId3 = curRunId3 + 1;
                if (curRunId3 > 255) {
                    curRunId3 = 0;
                    curRunId4 = curRunId4 + 1;
                    if (curRunId4 > 255) {
                        curRunId4 = 0;
                        curRunId5 = curRunId5 + 1;
                        if (curRunId5 > 255) {
                            curRunId5 = 0;
                            curRunId6 = curRunId6 + 1;
                            if (curRunId6 > 255) {
                                curRunId6 = 0;
                                curRunId7 = curRunId7 + 1;
                                if (curRunId7 > 255){
                                    curRunId0=0;curRunId1=0;curRunId2=0;curRunId3=0;curRunId4=0;curRunId5=0;curRunId6=0;curRunId7=0;
                                }
                            }
                        }
                    }
                }
            }
        }
    }
};



Event evMatchingProcessStartTrigger;
Event evTP_EV_SLAC_init_timeout;

Guard gRetryAfterWaitForCMSlacParmCnfsFailed;
Systemstate RetriesAfterWaitForCMSlacParmCnfsFailed;
RetriesAfterWaitForCMSlacParmCnfsFailed = 1025;
gRetryAfterWaitForCMSlacParmCnfsFailed = RetriesAfterWaitForCMSlacParmCnfsFailed > 0;

sm{
    EV;
    states{Initial;WaitForMatchingProcessStartTrigger;SendCMSlacParmReq;WaitForCMSlacParmCnfs; WaitForCMSlacParmCnfsFailed; Final; };
    Actions{
        doSendCM_SLAC_PARM_REQ{
            next_run_id();

            send_homeplug_mme(
              mme{ 
                    header{
                        oda{1;2.5;"ABCDEF";80;90;100;110;120;130;};
                        osa{2;4;6;8;10;12;};
                        vlan_tag{233;};
                        mtype{256;};
                        mmv{257;};
                        mmtype{CM_SLAC_PARM_REQ;};
                        fmi{2;};
                        fmsn{3;};
                    }; 
                    payload{
                        application_type{2;};
                        security_type{1;};
                        run_id{curRunId0;curRunId1;curRunId2;curRunId3;curRunId4;curRunId5;curRunId6;curRunId7;};
                    };
                    debug{
                        print_message{stdout;};
                    }; 
              } 
            );
        };
        doStart_TP_EV_SLAC_init_timer{
            start_timer(TP_EV_SLAC_init,evTP_EV_SLAC_init_timeout,TP_EV_SLAC_init_timer);
        };
        onWaitForCMSlacParmCnfsFailed{
            RetriesAfterWaitForCMSlacParmCnfsFailed = RetriesAfterWaitForCMSlacParmCnfsFailed - 1;
        };
    };
    t{Initial;WaitForMatchingProcessStartTrigger;};
    t{WaitForMatchingProcessStartTrigger;SendCMSlacParmReq;evMatchingProcessStartTrigger;};
    t{SendCMSlacParmReq;WaitForCMSlacParmCnfs;doSendCM_SLAC_PARM_REQ;doStart_TP_EV_SLAC_init_timer;};
    t{WaitForCMSlacParmCnfs;WaitForCMSlacParmCnfs;evCM_SLAC_PARM_CNF;};
    t{WaitForCMSlacParmCnfs; WaitForCMSlacParmCnfsFailed; evTP_EV_SLAC_init_timeout;onWaitForCMSlacParmCnfsFailed;};
    t{WaitForCMSlacParmCnfsFailed;SendCMSlacParmReq;gRetryAfterWaitForCMSlacParmCnfsFailed;};
    t{WaitForCMSlacParmCnfsFailed;Initial;!gRetryAfterWaitForCMSlacParmCnfsFailed;};
};

Event evMMEPluginError;

sm{
    Watchdog;
    states{Initial;Error;};
    t{Initial;Error;evMMEPluginError;};
};

Simulation{
    Event EXIT;
    start_timer(60.0*s,EXIT);
    run_include_homeplug_mme_handler{};
    Start{EV;Watchdog;};
    start_timer(2.0*s, evMatchingProcessStartTrigger);
};